Program.Sub.ScreenSU.Start
gui.SS_Form1..create
gui.SS_Form1..caption("Lot Number")
gui.SS_Form1..size(2250,2180)
gui.SS_Form1..position(0,0)
gui.SS_Form1..alwaysontop(False)
gui.SS_Form1..fontname("Arial")
gui.SS_Form1..fontsize(8)
gui.SS_Form1..forecolor(0)
gui.SS_Form1..fontstyle(,,,,)
gui.SS_Form1..BackColor(-2147483633)
gui.SS_Form1..controlbox(True)
gui.SS_Form1..maxbutton(False)
gui.SS_Form1..minbutton(False)
gui.SS_Form1..mousepointer(0)
gui.SS_Form1..moveable(True)
gui.SS_Form1..sizeable(False)
gui.SS_Form1..ShowInTaskBar(True)
gui.SS_Form1..titlebar(True)
gui.SS_Form1..event(unload,ss_form1_unload)
gui.SS_Form1.lblJob.create(label,"",True,1800,200,1,100,135,True,0,Arial,8,-2147483633,0)
gui.SS_Form1.lbl_txtLot.create(label,"Lot Number",True,1800,200,1,100,505,True,0,Arial,8,-2147483633,0)
gui.SS_Form1.txtLot.create(textbox,"",True,1800,330,1,100,705,True,0,Arial,8,16777215,1)
gui.SS_Form1.cmdSave.create(button)
gui.SS_Form1.cmdSave.Caption("Save")
gui.SS_Form1.cmdSave.visible(True)
gui.SS_Form1.cmdSave.size(1000,360)
gui.SS_Form1.cmdSave.position(100,1205)
gui.SS_Form1.cmdSave.enabled(True)
gui.SS_Form1.cmdSave.fontname("Arial")
gui.SS_Form1.cmdSave.fontsize(8)
gui.SS_Form1.cmdSave.event(click,cmdsave_click)
gui.SS_Form1.txtLot.tabstop(true)
gui.SS_Form1.txtLot.tabindex(1)
gui.SS_Form1.cmdSave.tabstop(true)
gui.SS_Form1.cmdSave.tabindex(2)


Program.Sub.ScreenSU.End

Program.Sub.Preflight.Start

Program.Sub.Preflight.End

Program.Sub.Main.Start
F.Intrinsic.Control.SetErrorHandler("Main_Err")
F.Intrinsic.Control.ClearErrors
'================================

V.Local.sError.Declare(String)
V.Local.sSel.Declare(String)
V.Local.sRet.Declare(String)
V.Local.sVal.Declare(String)

' if not on the po process, nothing will happen if qtys are not entered
F.Intrinsic.Control.If(V.Caller.Hook,<>,15902)
	F.Intrinsic.Control.If(V.Passed.000001,=,".000000")
	F.Intrinsic.Control.AndIf(V.Passed.000002,=,".000000")
		F.Intrinsic.Control.End
	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.EndIf


F.ODBC.Connection!Con.OpenConnection(V.Ambient.PDSN,V.Ambient.PUser,V.Ambient.PPass)

F.Intrinsic.String.Concat("Job: ",V.Passed.009990," - ",V.Passed.009991,V.Local.sVal)

' see if lot has been set for job
F.Intrinsic.String.Concat("select User_1 from WO_User_Flds where  Job='",V.passed.009990,"' and Suffix='",V.Passed.009991,"'",V.Local.sSel)
Function.ODBC.Connection!Con.ExecuteAndReturn(V.Local.sSel,4,V.Local.sRet)
	F.Intrinsic.Control.If(V.Local.sRet.Trim,<>,"")
		F.Intrinsic.String.Split(V.Local.sRet,"*!*",V.Local.sRet)
	F.Intrinsic.Control.EndIf

' if called from update job populate hook
F.Intrinsic.Control.If(V.Caller.Hook,=,"15840")
	F.Intrinsic.Control.CallSub(Screentext)
	F.Intrinsic.Control.CallSub(Unload)

' if called from the script1 button on GUI update job
F.Intrinsic.Control.elseIf(V.Caller.Hook,=,"15850")
	F.Intrinsic.Control.If(V.Local.sRet(0).Trim,=,"")
		Gui.SS_Form1.lblJob.Caption(V.Local.sVal)
		Gui.SS_Form1..Show
	F.Intrinsic.Control.Else
		F.Intrinsic.Control.CallSub(Labeldata)
		F.Intrinsic.Control.CallSub(Printlabel)
		F.Intrinsic.Control.CallSub(Unload)
	F.Intrinsic.Control.EndIf

' if called from the Process button on update job
F.Intrinsic.Control.ElseIf(V.Caller.Hook,=,"15890")
	F.Intrinsic.Control.If(V.Local.sRet(0).Trim,=,"")
		Gui.SS_Form1.lblJob.Caption(V.Local.sVal)
		Gui.SS_Form1..Show
	F.Intrinsic.Control.Else
		F.Intrinsic.Control.CallSub(Labeldata)
		F.Intrinsic.Control.CallSub(Printlabel)
		F.Intrinsic.Control.CallSub(Unload)
	F.Intrinsic.Control.EndIf

' if called from the Process button on update multiple jobs
F.Intrinsic.Control.ElseIf(V.Caller.Hook,=,"15915")
	F.Intrinsic.Control.If(V.Local.sRet(0).Trim,=,"")
		Gui.SS_Form1.lblJob.Caption(V.Local.sVal)
		Gui.SS_Form1..Show
	F.Intrinsic.Control.Else
		F.Intrinsic.Control.CallSub(Labeldata)
		F.Intrinsic.Control.CallSub(Printlabel)
		F.Intrinsic.Control.CallSub(Unload)
	F.Intrinsic.Control.EndIf

' if called from the script2 button
F.Intrinsic.Control.ElseIf(V.Caller.Hook,=,15860)
	F.Intrinsic.Control.If(V.Local.sRet(0).Trim,<>,"")
		Gui.SS_Form1.txtLot.Text(V.Local.sRet(0).Trim)
	F.Intrinsic.Control.EndIf
	Gui.SS_Form1.lblJob.Caption(V.Local.sVal)
	Gui.SS_Form1..Show

' if called from the request po process button
F.Intrinsic.Control.ElseIf(V.Caller.Hook,=,15902)
	F.Intrinsic.Control.If(V.Local.sRet(0).Trim,<>,"")
		Gui.SS_Form1.txtLot.Text(V.Local.sRet(0).Trim)
	F.Intrinsic.Control.EndIf
	Gui.SS_Form1.lblJob.Caption(V.Local.sVal)
	Gui.SS_Form1..Show

F.Intrinsic.Control.EndIf


'================================
F.Intrinsic.Control.Label("Main_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_Print_Report_WIP_Operation_Label_Var3.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf


Program.Sub.Main.End

Program.Sub.Unload.Start

F.ODBC.Connection!Con.Close
F.Intrinsic.Control.End

Program.Sub.Unload.End

Program.Sub.screenText.Start
F.Intrinsic.Control.SetErrorHandler("screenText_Err")
F.Intrinsic.Control.ClearErrors
'================================

V.Local.sError.Declare(String)



Variable.passed.000048.set("Move Ticket")
V.Passed.000049.Set("Lot #")

F.Intrinsic.Control.ExitSub

'================================
F.Intrinsic.Control.Label("screenText_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_Print_Report_WIP_Operation_Label_Var3.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf


Program.Sub.screenText.End

Program.Sub.labelData.Start
F.Intrinsic.Control.SetErrorHandler("labelData_Err")
F.Intrinsic.Control.ClearErrors
'================================

V.Local.sError.Declare(String)
V.Local.sSel.Declare(String)
V.Local.sEmployee.Declare(String)
V.Local.sQualityNumber.Declare(String)
V.Local.sCurPart.Declare(String)
V.Local.sCurDescr.Declare(String)
V.Local.sQtyCompleted.Declare(String)
V.Local.sNextSeq.Declare(String)
V.Local.sNextPart.Declare(String)
V.Local.sNextDescr.Declare(String)
V.Local.sNextLmo.Declare(String)
V.Local.sName.Declare(String)
V.Local.sCust.Declare(String)
V.Local.sCustPart.Declare(String)
V.Local.sLot.Declare(String)
V.Local.sRet.Declare(String)
V.Local.sPart.Declare(String)
V.Local.sVendor.Declare(String)

'verify trmnl is on ATG_Move_Tckt_Setup to be able to print label/comments
'F.Intrinsic.String.Concat("select * from ATG_MOVE_TCKT_SETUP where TRMNL='",V.Caller.Terminal,"'",V.Local.sSel)
'F.ODBC.Connection!Con.OpenRecordsetRO("rstX",V.Local.sSel)
'	F.Intrinsic.Control.If(V.ODBC.Con!rstX.EOF,=,True)
'		F.Intrinsic.Control.CallSub(Unload)
'	F.Intrinsic.Control.EndIf
'F.ODBC.Con!rstX.Close

F.Intrinsic.String.Concat("delete from GCG_3339_WIP_Oper_Lbl3 where TRMNL='",V.Caller.Terminal,"'",V.Local.sSel)
F.ODBC.Connection!Con.Execute(V.Local.sSel)

' if good/scrap pieces are both 0 then don't continue
F.Intrinsic.Control.If(V.Passed.000001,=,".000000")
F.Intrinsic.Control.AndIf(V.Passed.000002,=,".000000")
	F.Intrinsic.Control.CallSub(Unload)
F.Intrinsic.Control.EndIf

F.Intrinsic.String.Concat("select SEQ,PART,DESCRIPTION,LMO,Operation from V_JOB_OPERATIONS where JOB='",V.passed.009990,"' and SUFFIX='",V.Passed.009991,"' and SEQ>'",V.Passed.009992,"' and LMO <> 'C'  and LMO <> 'M' order by SEQ",V.Local.sSel)
Function.ODBC.Connection!Con.ExecuteAndReturn(V.Local.sSel,1,V.Local.sRet)
	F.Intrinsic.Control.If(V.Local.sRet.Trim,<>,"")
		F.Intrinsic.String.Split(V.Local.sRet,"*!*",V.Local.sRet)
		V.Local.sRet.RedimPreserve(0,4)
		V.Local.sNextSeq.Set(V.Local.sRet(0))
		V.Local.sNextPart.Set(V.Local.sRet(1))
		V.Local.sNextDescr.Set(V.Local.sRet(2))
		V.Local.sNextLMO.Set(V.Local.sRet(3))
		V.Local.sVendor.Set(V.Local.sRet(4))
	F.Intrinsic.Control.EndIf

F.Intrinsic.String.Concat("select Customer, Part_Customer,Part from V_JOB_HEADER where Job='",V.passed.009990,"' and SUFFIX='",V.Passed.009991,"'",V.Local.sSel)
Function.ODBC.Connection!Con.ExecuteAndReturn(V.Local.sSel,1,V.Local.sRet)
	F.Intrinsic.Control.If(V.Local.sRet.Trim,<>,"")
		F.Intrinsic.String.Split(V.Local.sRet,"*!*",V.Local.sRet)
		V.Local.sRet.RedimPreserve(0,2)
		V.Local.sCust.Set(V.Local.sRet(0))
		V.Local.sCustPart.Set(V.Local.sRet(1))
		V.Local.sPart.Set(V.Local.sRet(2))
	F.Intrinsic.Control.EndIf

F.Intrinsic.String.Concat("select NAME_CUSTOMER from V_CUSTOMER_MASTER where CUSTOMER='",V.Local.sCust.Trim,"'",V.Local.sSel)
Function.ODBC.Connection!Con.ExecuteAndReturn(V.Local.sSel,1,V.Local.sRet)
	F.Intrinsic.Control.If(V.Local.sRet.Trim,<>,"")
		F.Intrinsic.String.Split(V.Local.sRet,"*!*",V.Local.sRet)
		V.Local.sName.Set(V.Local.sRet(0))
	F.Intrinsic.Control.Endif

F.Intrinsic.String.Concat("select * from GCG_3339_WIP_Oper_Lbl3 where TRMNL='",V.Caller.Terminal,"'",V.Local.sSel)
F.ODBC.Connection!Con.OpenRecordsetRW("rstSel",V.Local.sSel)
	F.ODBC.Con!rstSel.AddNew
		F.ODBC.Con!rstSel.Set!TRMNL(V.Caller.Terminal)
		F.ODBC.Con!rstSel.Set!JOB(V.passed.009990)
		F.ODBC.Con!rstSel.Set!SUFFIX(V.Passed.009991)
		F.ODBC.Con!rstSel.Set!SEQ(V.Passed.009992)
		F.ODBC.Con!rstSel.Set!Part(V.Local.sPart)
		F.ODBC.Con!rstSel.Set!Workcenter(V.Passed.000010)
		F.ODBC.Con!rstSel.Set!QTY(V.Passed.000001)
		F.ODBC.Con!rstSel.Set!Qty_Scrap(V.Passed.000002)
		F.ODBC.Con!rstSel.Set!EMPLOYEE(V.Passed.009994)
		F.ODBC.Con!rstSel.Set!CLOSE_FLAG(V.Passed.000003)
		F.ODBC.Con!rstSel.Set!CUSTOMER(V.Local.sCust.Trim)
		F.ODBC.Con!rstSel.Set!Part_Customer(V.Local.sCustPart.Trim)
		F.ODBC.Con!rstSel.Set!NAME_Cust(V.Local.sName.Trim)
		F.ODBC.Con!rstSel.Set!NEXT_SEQ(V.Local.sNextSeq.Trim)
		F.ODBC.Con!rstSel.Set!NEXT_PART(V.Local.sNextPart.Trim)
		F.ODBC.Con!rstSel.Set!NEXT_DESCR(V.Local.sNextDescr.Trim)
		F.ODBC.Con!rstSel.Set!Next_LMO(V.Local.sNextLMO.Trim)
		F.ODBC.Con!rstSel.Set!Vendor(V.Local.sVendor.Trim)
	F.ODBC.Con!rstSel.Update
F.ODBC.Con!rstSel.Close


F.Intrinsic.Control.ExitSub

'================================
F.Intrinsic.Control.Label("labelData_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_Print_Report_WIP_Operation_Label_Var3.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf


Program.Sub.labelData.End

Program.Sub.printLabel.Start
F.Intrinsic.Control.SetErrorHandler("printLabel_Err")
F.Intrinsic.Control.ClearErrors
'================================

V.Local.sError.Declare(String)
V.Local.sRptLoc.Declare(String)
V.Local.iRptId.Declare(Long)
V.Local.sParams.Declare(String)
V.Local.sValues.Declare(String)
V.Local.sScrapflag.Declare(String)


F.Intrinsic.String.Concat(V.Caller.BusintDir,"\Custom\WIP_3339_Operation_Label_Var3.rpt",V.Local.srptLoc)
F.Global.BI.InitializeReport("WIP_Operation_Label_ATG",V.Local.srptLoc,V.Local.iRptId)

F.Intrinsic.Control.If(V.Passed.000001,<>,".000000")
	V.Local.sScrapflag.Set("N")
		F.Intrinsic.String.Split("TRMNL*!*Scrapflag","*!*",V.Local.sParams)
		F.Intrinsic.String.Concat(V.Caller.Terminal,"*!*",V.Local.sScrapflag,V.Local.sValues)
		F.Intrinsic.String.Split(V.Local.sValues,"*!*",V.Local.sValues)
'	F.Intrinsic.Control.If(V.Caller.Hook,=,15915)
'		F.Global.BI.PrintReport(V.Local.iRptId,0,V.Local.sParams,V.Local.sValues,"",True)
'	F.Intrinsic.Control.Else
		F.Global.BI.PrintReport(V.Local.iRptId,0,V.Local.sParams,V.Local.sValues,"",False)
'	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.Endif

F.Intrinsic.Control.If(V.Passed.000002,<>,".000000")
	V.Local.sScrapflag.Set("Y")
		F.Intrinsic.String.Split("TRMNL*!*Scrapflag","*!*",V.Local.sParams)
		F.Intrinsic.String.Concat(V.Caller.Terminal,"*!*",V.Local.sScrapflag,V.Local.sValues)
		F.Intrinsic.String.Split(V.Local.sValues,"*!*",V.Local.sValues)
'	F.Intrinsic.Control.If(V.Caller.Hook,=,15915)
'		F.Global.BI.PrintReport(V.Local.iRptId,0,V.Local.sParams,V.Local.sValues,"",True)
'	F.Intrinsic.Control.Else
		F.Global.BI.PrintReport(V.Local.iRptId,0,V.Local.sParams,V.Local.sValues,"",False)
'	F.Intrinsic.Control.EndIf
F.Intrinsic.Control.Endif


F.Intrinsic.Control.ExitSub

'================================
F.Intrinsic.Control.Label("printLabel_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_Print_Report_WIP_Operation_Label_Var3.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf


Program.Sub.printLabel.End

program.sub.cmdsave_click.start
F.Intrinsic.Control.SetErrorHandler("cmdsave_click_Err")
F.Intrinsic.Control.ClearErrors
'================================

Gui.SS_Form1..Visible(false)

V.Local.sError.Declare(String)
V.Local.sSel.Declare(String)


F.Intrinsic.String.Concat("select * from WO_User_Flds where Job='",V.passed.009990,"' and Suffix='",V.Passed.009991,"' ",V.Local.sSel)
F.ODBC.Connection!Con.OpenRecordsetRW("rstSel",V.Local.sSel)
	F.Intrinsic.Control.If(V.ODBC.Con!rstSel.EOF,=,True)
		F.ODBC.Con!rstSel.AddNew
		F.ODBC.Con!rstSel.Set!Job(V.passed.009990)
		F.ODBC.Con!rstSel.Set!Suffix(V.Passed.009991)
	F.Intrinsic.Control.EndIf
		F.ODBC.Con!rstSel.Set!User_1(V.Screen.SS_Form1!txtLot.Text)
		F.ODBC.Con!rstSel.Update
F.ODBC.Con!rstSel.Close

' if not called from the script2 or PO process hooks (only to change lot, not print label)
F.Intrinsic.Control.If(V.Caller.Hook,<>,15860)
F.Intrinsic.Control.AndIf(V.Caller.Hook,<>,15902)
	F.Intrinsic.Control.CallSub(Labeldata)
	F.Intrinsic.Control.CallSub(Printlabel)
F.Intrinsic.Control.EndIf

F.Intrinsic.Control.CallSub(Unload)

'================================
F.Intrinsic.Control.Label("cmdsave_click_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_Print_Report_WIP_Operation_Label_Var3.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf


program.sub.cmdsave_click.end

program.sub.ss_form1_unload.start
F.Intrinsic.Control.SetErrorHandler("ss_form1_unload_Err")
F.Intrinsic.Control.ClearErrors
'================================

V.Local.sError.Declare(String)
V.Local.sRet.Declare(String)

F.Intrinsic.Control.If(V.Screen.SS_Form1!txtLot.Text,=,"")
	F.Intrinsic.UI.Msgbox("Lot# missing.  Exit without saving?","",4,V.Local.sRet)

	' if yes to exit
	F.Intrinsic.Control.If(V.Local.sRet,=,6)
		' from the Lot# button
		F.Intrinsic.Control.If(V.Caller.Hook,=,15860)
			F.Intrinsic.Control.CallSub(Unload)
		F.Intrinsic.Control.Elseif(V.Caller.Hook,=,15902)
			F.Intrinsic.Control.CallSub(Unload)
		F.Intrinsic.Control.Else
			Gui.SS_Form1..Visible(false)
			F.Intrinsic.Control.CallSub(Labeldata)
			F.Intrinsic.Control.CallSub(Printlabel)
			F.Intrinsic.Control.CallSub(Unload)
		F.Intrinsic.Control.EndIf

	' if no to exit, exit sub so they can enter a lot
	F.Intrinsic.Control.Else
		F.Intrinsic.Control.ExitSub
	F.Intrinsic.Control.EndIf

' lot number was found so go ahead and exit
F.Intrinsic.Control.Else
	F.Intrinsic.Control.CallSub(Unload)
F.Intrinsic.Control.EndIf


'================================
F.Intrinsic.Control.Label("ss_form1_unload_Err")
F.Intrinsic.Control.If(V.Ambient.ErrorNumber,<>,0)
	Function.Intrinsic.String.Concat("Project: ATG_Print_Report_WIP_Operation_Label_Var3.gas",V.Ambient.Newline,V.Ambient.Newline,"Subroutine: ",V.Ambient.CurrentSubroutine,V.Ambient.NewLine,"Error Occurred ",V.Ambient.ErrorNumber," with description ",V.Ambient.ErrorDescription,V.Local.sError)
	F.Intrinsic.UI.Msgbox(V.Local.sError)
Function.Intrinsic.Control.EndIf


program.sub.ss_form1_unload.end

Program.Sub.Comments.Start
${$0$}$ATG_Print_Report_WIP_Operation_Labe$}$JCT$}$3/13/2013
${$1$}$$}$$}$5$}$15890$}$OLS003-PB-PROCESS-POST-HOOK (On-line GUI)$}$3/14/2013 11:20:48 AM$}$(Program: OLG002; Screen: OLS003)
single job process button to print move ticket
${$1$}$$}$$}$4$}$15915$}$OLS003M-PB-PROCESS-PRE-HOOK (On-line GUI)$}$3/14/2013 11:19:49 AM$}$(Program: OLG002; Screen: OLS003M)
multi line process to print move ticket
${$1$}$$}$$}$3$}$15850$}$OLS003-PB-SCRIPT-1-HOOK (On-line GUI)$}$3/14/2013 11:19:26 AM$}$(Program: OLG002; Screen: OLS003)
script1 to print move ticket
${$1$}$$}$$}$2$}$15902$}$OLS003O Pre-Process$}$3/13/2013 2:34:17 PM$}$(Program: OLG002; Screen: OLS003O)
Outside Op "process" button - text box to enter Lot#
${$1$}$$}$$}$1$}$15860$}$OLS003-PB-SCRIPT-2-HOOK (On-line GUI)$}$3/13/2013 2:33:40 PM$}$(Program: OLG002; Screen: OLS003)
text box to enter Lot#
${$1$}$$}$$}$0$}$15840$}$OLS003-POPULATE-HOOK (On-line GUI)$}$3/13/2013 2:31:28 PM$}$(Program: OLG002; Screen: OLS003)
Change Script1 to read "Move Ticket"; Script2 to read "Lot#"
${$2$}$$}$$}$1$}$4$}$rpt$}$3/13/2013 2:36:19 PM$}$WIP_Operation_Label_Var3.rpt
${$3$}$0$}$$}$-1$}$-1$}$$}$1/1/1900$}$custom version for GFM Corporation
Program.Sub.Comments.End
Program.Sub.ScreenSS.Start
SS_Form1{{CAPTION::Lot Number
SS_Form1.CTRL{{NAME::lblJob\\TYPE::1\\GROUP::1\\BROWSER::0\\SIZE::1
SS_Form1.CTRL{{NAME::txtLot\\TYPE::2\\CAPTION::Lot Number\\TABSTOP::1\\GROUP::2\\BROWSER::0\\SIZE::1
SS_Form1.CTRL{{NAME::cmdSave\\TYPE::5\\CAPTION::Save\\TABSTOP::2\\GROUP::3\\BROWSER::0\\SIZE::1

Program.Sub.ScreenSS.End

